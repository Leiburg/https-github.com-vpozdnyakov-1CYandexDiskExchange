#Область ПрограммныйИнтерфейс

Процедура ПолучитьТокен() Экспорт
	
	ИмяСервера = "oauth.yandex.ru";
	ОтносительныйURL = "/token";
	
	ИмяВременногоФайла = ПолучитьИмяВременногоФайла();
	ЗаписьТекста = Новый ЗаписьТекста(ИмяВременногоФайла, КодировкаТекста.UTF8);	
	ЗаписьТекста.Записать(IDПриложения + ":" + ПарольПриложения);
	ЗаписьТекста.Закрыть();
	
	СтрокаЗапроса = "grant_type=authorization_code" +
		"&code=" 			+ КодАвторизации + 
		"&client_id=" 		+ IDПриложения + 
		"&client_secret=" 	+ ПарольПриложения;
	
	Заголовки = Новый Соответствие;
	Заголовки.Вставить("Content-Type", "application/x-www-form-urlencoded");
	Заголовки.Вставить("Content-Length", XMLСтрока(СтрДлина(СтрокаЗапроса)));
	
	HttpЗапрос = Новый HTTPЗапрос(ОтносительныйURL, Заголовки);
	HttpЗапрос.УстановитьТелоИзСтроки(СтрокаЗапроса);
	
	HttpСоединение = Новый HTTPСоединение(ИмяСервера, 443,,,,, Новый ЗащищенноеСоединениеOpenSSL);
	HttpОтвет = HttpСоединение.ОтправитьДляОбработки(HttpЗапрос);

	ТелоОтвета = HttpОтвет.ПолучитьТелоКакСтроку(КодировкаТекста.UTF8);
	ОбработатьКодСостояния(HttpОтвет, ОтносительныйURL, ТелоОтвета);
	
	Чтение = Новый ЧтениеJSON;
	Чтение.УстановитьСтроку(ТелоОтвета);
	Ответ = ФабрикаXDTO.ПрочитатьJSON(Чтение);
	
	Токен = Ответ.access_token;
	СрокДействияТокена = ТекущаяДата() + Ответ.expires_in;
	
КонецПроцедуры

Процедура ПолучитьСписокФайлов(Знач Путь = "/") Экспорт
	
	СписокФайлов.Очистить();
	
	ИмяСервера = "cloud-api.yandex.net";
	ОтносительныйURL = "/v1/disk/resources?path=" + 
		КодироватьСтроку(Путь, СпособКодированияСтроки.КодировкаURL) +
		"&limit=" + XMLСтрока(КоличествоЭлементовНаСтранице);
	
	Заголовки = Новый Соответствие;
	Заголовки.Вставить("Content-Type", "application/json");
	Заголовки.Вставить("Accept", "application/json");
	Если ТекущаяДата() > СрокДействияТокена Тогда
		ПолучитьТокен();
	КонецЕсли;
	Заголовки.Вставить("Authorization", "OAuth " + Токен);
	
	HttpЗапрос = Новый HTTPЗапрос(ОтносительныйURL, Заголовки);
	
	HttpСоединение = Новый HTTPСоединение(ИмяСервера, 443,,,,, Новый ЗащищенноеСоединениеOpenSSL);
	HttpОтвет = HttpСоединение.Получить(HttpЗапрос);

	ТелоОтвета = HttpОтвет.ПолучитьТелоКакСтроку(КодировкаТекста.UTF8);
	ОбработатьКодСостояния(HttpОтвет, ОтносительныйURL, ТелоОтвета);
	
	Чтение = Новый ЧтениеJSON;
	Чтение.УстановитьСтроку(ТелоОтвета);
	Ответ = ФабрикаXDTO.ПрочитатьJSON(Чтение);
	
	Если Ответ._embedded.total > 1 Тогда
		Для Каждого Item Из Ответ._Embedded.Items.Items Цикл
			Стр = СписокФайлов.Добавить();
			Стр.Имя = Item.Name;
			Стр.Тип = Item.Type;
			Стр.Путь = Item.Path;
		КонецЦикла;
	ИначеЕсли Ответ._embedded.total = 1 Тогда
		Стр = СписокФайлов.Добавить();
		Стр.Имя = Ответ._Embedded.Items.Items.Name;
		Стр.Тип = Ответ._Embedded.Items.Items.Type;
		Стр.Путь = Ответ._Embedded.Items.Items.Path;
	КонецЕсли;
	
КонецПроцедуры

Процедура СоздатьПапку(Знач Путь) Экспорт
	
	ИмяСервера = "cloud-api.yandex.net";
	ОтносительныйURL = "/v1/disk/resources?path=" + 
		КодироватьСтроку(Путь, СпособКодированияСтроки.КодировкаURL);
	
	Заголовки = Новый Соответствие;
	Заголовки.Вставить("Content-Type", "application/json");
	Заголовки.Вставить("Accept", "application/json");
	Если ТекущаяДата() > СрокДействияТокена Тогда
		ПолучитьТокен();
	КонецЕсли;
	Заголовки.Вставить("Authorization", "OAuth " + Токен);
	
	HttpЗапрос = Новый HTTPЗапрос(ОтносительныйURL, Заголовки);
	HttpЗапрос.УстановитьТелоИзСтроки("");
	
	HttpСоединение = Новый HTTPСоединение(ИмяСервера, 443,,,,, Новый ЗащищенноеСоединениеOpenSSL);
	HttpОтвет = HttpСоединение.Записать(HTTPЗапрос);

	ТелоОтвета = HttpОтвет.ПолучитьТелоКакСтроку(КодировкаТекста.UTF8);
	ОбработатьКодСостояния(HttpОтвет, ОтносительныйURL, ТелоОтвета);
	
КонецПроцедуры

Процедура УдалитьПапкуИлиФайл(Знач Путь) Экспорт
	
	СписокФайлов.Очистить();
	
	ИмяСервера = "cloud-api.yandex.net";
	ОтносительныйURL = "/v1/disk/resources?path=" + 
		КодироватьСтроку(Путь, СпособКодированияСтроки.КодировкаURL);
	
	Заголовки = Новый Соответствие;
	Заголовки.Вставить("Content-Type", "application/json");
	Заголовки.Вставить("Accept", "application/json");
	Если ТекущаяДата() > СрокДействияТокена Тогда
		ПолучитьТокен();
	КонецЕсли;
	Заголовки.Вставить("Authorization", "OAuth " + Токен);
	
	HttpЗапрос = Новый HTTPЗапрос(ОтносительныйURL, Заголовки);
	
	HttpСоединение = Новый HTTPСоединение(ИмяСервера, 443,,,,, Новый ЗащищенноеСоединениеOpenSSL);
	HttpОтвет = HttpСоединение.Удалить(HttpЗапрос);

	ТелоОтвета = HttpОтвет.ПолучитьТелоКакСтроку(КодировкаТекста.UTF8);
	ОбработатьКодСостояния(HttpОтвет, ОтносительныйURL, ТелоОтвета);
	
КонецПроцедуры

Процедура ЗагрузитьФайлНаДиск(Знач Путь, Знач АдресХранилища) Экспорт
	
	ИмяСервера = "cloud-api.yandex.net";
	ОтносительныйURL = "/v1/disk/resources/upload?path=" + 
		КодироватьСтроку(Путь, СпособКодированияСтроки.КодировкаURL);
	
	Заголовки = Новый Соответствие;
	Заголовки.Вставить("Content-Type", "application/json");
	Заголовки.Вставить("Accept", "application/json");
	Если ТекущаяДата() > СрокДействияТокена Тогда
		ПолучитьТокен();
	КонецЕсли;
	Заголовки.Вставить("Authorization", "OAuth " + Токен);
	
	HttpЗапрос = Новый HTTPЗапрос(ОтносительныйURL, Заголовки);
	
	HttpСоединение = Новый HTTPСоединение(ИмяСервера, 443,,,,, Новый ЗащищенноеСоединениеOpenSSL);
	HttpОтвет = HttpСоединение.Получить(HTTPЗапрос);

	ТелоОтвета = HttpОтвет.ПолучитьТелоКакСтроку(КодировкаТекста.UTF8);
	ОбработатьКодСостояния(HttpОтвет, ОтносительныйURL, ТелоОтвета);
	
	Чтение = Новый ЧтениеJSON;
	Чтение.УстановитьСтроку(ТелоОтвета);
	Ответ = ФабрикаXDTO.ПрочитатьJSON(Чтение);
	
	СтруктураURL = РазделитьURL(Ответ.href);
	HttpСоединение = Новый HTTPСоединение(СтруктураURL.ИмяСервера, СтруктураURL.Порт,,,,, Новый ЗащищенноеСоединениеOpenSSL);
	HttpЗапрос = Новый HTTPЗапрос(СтруктураURL.Путь, Заголовки);
	HttpЗапрос.УстановитьТелоИзДвоичныхДанных(ПолучитьИзВременногоХранилища(АдресХранилища));
	HttpОтвет = HttpСоединение.Записать(HTTPЗапрос);
	
	ОбработатьКодСостояния(HttpОтвет, СтруктураURL.Путь);
	
КонецПроцедуры

Функция ПолучитьДанныеФайла(Знач Путь) Экспорт
	
	ИмяСервера = "cloud-api.yandex.net";
	ОтносительныйURL = "/v1/disk/resources/download?path=" + 
		КодироватьСтроку(Путь, СпособКодированияСтроки.КодировкаURL);
	
	Заголовки = Новый Соответствие;
	Заголовки.Вставить("Content-Type", "application/json");
	Заголовки.Вставить("Accept", "application/json");
	Если ТекущаяДата() > СрокДействияТокена Тогда
		ПолучитьТокен();
	КонецЕсли;
	Заголовки.Вставить("Authorization", "OAuth " + Токен);
	
	HttpЗапрос = Новый HTTPЗапрос(ОтносительныйURL, Заголовки);
	
	HttpСоединение = Новый HTTPСоединение(ИмяСервера, 443,,,,, Новый ЗащищенноеСоединениеOpenSSL);
	HttpОтвет = HttpСоединение.Получить(HttpЗапрос);

	ТелоОтвета = HttpОтвет.ПолучитьТелоКакСтроку(КодировкаТекста.UTF8);
	ОбработатьКодСостояния(HttpОтвет, ОтносительныйURL, ТелоОтвета);
	
	Чтение = Новый ЧтениеJSON;
	Чтение.УстановитьСтроку(ТелоОтвета);
	Ответ = ФабрикаXDTO.ПрочитатьJSON(Чтение);

	СтруктураURL = РазделитьURL(Ответ.href);
	HttpСоединение = Новый HTTPСоединение(СтруктураURL.ИмяСервера, СтруктураURL.Порт,,,,, Новый ЗащищенноеСоединениеOpenSSL);
	HttpЗапрос = Новый HTTPЗапрос(СтруктураURL.Путь, Заголовки);
	HttpОтвет = HttpСоединение.Получить(HttpЗапрос);
		
	// редирект
	Пока HttpОтвет.КодСостояния = 302 Цикл
		СтруктураURL = РазделитьURL(HttpОтвет.Заголовки.Получить("Location"));
		HttpСоединение = Новый HTTPСоединение(СтруктураURL.ИмяСервера, СтруктураURL.Порт,,,,, Новый ЗащищенноеСоединениеOpenSSL);
		HttpЗапрос = Новый HTTPЗапрос(СтруктураURL.Путь, Заголовки);
		HttpОтвет = HttpСоединение.Получить(HttpЗапрос);
	КонецЦикла;
	
	ОбработатьКодСостояния(HttpОтвет, СтруктураURL.Путь);
	
	ДанныеФайла = HttpОтвет.ПолучитьТелоКакДвоичныеДанные();
	Адрес = ПоместитьВоВременноеХранилище(ДанныеФайла);
	Возврат Адрес;
	
КонецФункции

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

Процедура ОбработатьКодСостояния(Знач HttpОтвет, Знач ОтносительныйURL, Знач ТелоОтвета = "")
	Перем ТекстИсключения;
	Если (HttpОтвет.КодСостояния < 200) Или (HttpОтвет.КодСостояния >= 300) Тогда
		ТекстИсключения = "Запрос: " + ОтносительныйURL + Символы.ПС;
		ТекстИсключения = ТекстИсключения + "Код ответа: " + HttpОтвет.КодСостояния + Символы.ПС;
		ТекстИсключения = ТекстИсключения + "Тело ответа: " + ТелоОтвета;
		ВызватьИсключение ТекстИсключения;
	КонецЕсли;
КонецПроцедуры

Функция РазделитьURL(Знач URL)
	
	ИмяСервера = "";
	Путь = "";
	Протокол = "";
	Порт = 443;
	
	URL = СокрЛП(URL);
	
	Если Лев(URL, 7) = "http://" Тогда
		URL = Прав(URL, СтрДлина(URL) - 7);
		Протокол = "http";
	ИначеЕсли Лев(URL, 8) = "https://" Тогда
		URL = Прав(URL, СтрДлина(URL) - 8);
		Протокол = "https";
	КонецЕсли;

	Индекс = СтрНайти(URL, "/");
	Если Индекс Тогда
		ИмяСервера = Лев(URL, Индекс - 1);
		Путь = Сред(URL, Индекс);
	КонецЕсли;
	
	Индекс = СтрНайти(ИмяСервера, ":");
	Если Индекс Тогда
		Порт = Число(Сред(ИмяСервера, Индекс + 1));
		ИмяСервера = Лев(ИмяСервера, Индекс - 1);
	КонецЕсли;
	
	Результат = Новый Структура;
	Результат.Вставить("ИмяСервера", ИмяСервера);
	Результат.Вставить("Путь", Путь);
	Результат.Вставить("Протокол", Протокол);
	Результат.Вставить("Порт", Порт);
	
	Возврат Результат;
	
КонецФункции

#КонецОбласти
