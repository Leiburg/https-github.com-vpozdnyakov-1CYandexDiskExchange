#Область ОбработчикиСобытийФормы

&НаКлиенте
Процедура ОбработкаОповещения(ИмяСобытия, Параметр, Источник)
	Если ИмяСобытия = "ПолучениеКодаАвторизации" Тогда
		Объект.КодАвторизации = Параметр;
	КонецЕсли;
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиКомандФормы

&НаКлиенте
Процедура ОбновитьСписокФайлов(Команда)
	ПолучитьСписокФайловНаКлиенте();
КонецПроцедуры

&НаКлиенте
Процедура ТекущееРасположениеПриИзменении(Элемент)
	ПолучитьСписокФайловНаКлиенте();
КонецПроцедуры

&НаКлиенте
Процедура СкачатьФайл(Команда)
	ТекущиеДанные = Элементы.СписокФайлов.ТекущиеДанные;
	Если ТекущиеДанные = Неопределено Или ТекущиеДанные.Тип = "dir" Тогда
		Возврат;
	КонецЕсли;
	Адрес = СкачатьФайлНаСервере(ТекущиеДанные.Путь);
	ПолучитьФайл(Адрес, ТекущиеДанные.Имя + ?(ТекущиеДанные.Тип = "dir", ".zip", ""), Истина);
КонецПроцедуры

&НаКлиенте
Процедура ЗагрузитьФайл(Команда)
	ДиалогВыбораФайла = Новый ДиалогВыбораФайла(РежимДиалогаВыбораФайла.Открытие);
	ДиалогВыбораФайла.МножественныйВыбор = Ложь;
	ОписаниеОповещение = Новый ОписаниеОповещения("ОбработкаВыбораФайла", ЭтаФорма);
	ДиалогВыбораФайла.Показать(ОписаниеОповещение);
КонецПроцедуры

&НаКлиенте
Процедура СоздатьПапку(Команда)
	ИмяПапки = "";
	ОписаниеОповещения = Новый ОписаниеОповещения("ОбработкаВводаИмениПапки", ЭтаФорма);
	ПоказатьВводСтроки(ОписаниеОповещения, ИмяПапки, "Введите имя папки", 255);
КонецПроцедуры

&НаКлиенте
Процедура УдалитьПапкуИлиФайл(Команда)
	ОписаниеОповещения = Новый ОписаниеОповещения("ОбработкаРешенияОбУдалении", ЭтаФорма);
	ПоказатьВопрос(ОписаниеОповещения, "Вы уверены?", РежимДиалогаВопрос.ДаНет);
КонецПроцедуры

&НаКлиенте
Процедура ПолучитьСписокФайлов(Команда)
	ТекущееРасположение = "disk:/";
	ПолучитьСписокФайловНаКлиенте();
КонецПроцедуры

&НаКлиенте
Процедура ПолучитьКодАвторизации(Команда)
	ПараметрыОткрытия = Новый Структура("IDПриложения", Объект.IDПриложения);
	ОткрытьФорму("Обработка.ОбменЯндексДиск.Форма.ФормаАвторизации", ПараметрыОткрытия, ЭтаФорма, УникальныйИдентификатор);
КонецПроцедуры

&НаКлиенте
Процедура ПоднятьсяВверх(Команда)
	ПредпоследнийСлэш = СтрНайти(ТекущееРасположение, "/", НаправлениеПоиска.СКонца,, 2);
	Если ПредпоследнийСлэш Тогда
		ТекущееРасположение = Лев(ТекущееРасположение, ПредпоследнийСлэш);
		ПолучитьСписокФайловНаСервере();
	КонецЕсли;
КонецПроцедуры

&НаКлиенте
Процедура ПолучитьТокен(Команда)
	ПолучитьТокенНаСервере();
КонецПроцедуры

&НаКлиенте
Процедура СписокФайловВыбор(Элемент, ВыбраннаяСтрока, Поле, СтандартнаяОбработка)
	ОткрытьПапкуНаКлиенте();
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиАсинхронныхОповещений

&НаКлиенте
Процедура ОбработкаВыбораФайла(ИменаВыбранныхФайлов, ДополнительныеПараметры) Экспорт
	Если ИменаВыбранныхФайлов = Неопределено Тогда
		Возврат;
	КонецЕсли;
	КраткоеИмяФайла = Сред(ИменаВыбранныхФайлов[0], СтрНайти(ИменаВыбранныхФайлов[0], "\", НаправлениеПоиска.СКонца) + 1);
	АдресХранилища = ПоместитьВоВременноеХранилище(Новый ДвоичныеДанные(ИменаВыбранныхФайлов[0]));
	ВыбранныйФайл = Новый Структура("Путь, АдресХранилища", ТекущееРасположение + КраткоеИмяФайла, АдресХранилища);
	ЗагрузитьФайлНаСервере(ВыбранныйФайл);
	ПодключитьОбработчикОжидания("ПолучитьСписокФайловНаКлиенте", 1, Истина);
КонецПроцедуры

&НаКлиенте
Процедура ОбработкаВводаИмениПапки(ИмяПапки, ДополнительныеПараметры) Экспорт
	Если ИмяПапки = Неопределено Тогда
		Возврат;
	КонецЕсли;
	СоздатьПапкуНаСервере(ТекущееРасположение + ИмяПапки);
	ПодключитьОбработчикОжидания("ПолучитьСписокФайловНаКлиенте", 1, Истина);
КонецПроцедуры

&НаКлиенте
Процедура ОбработкаРешенияОбУдалении(РезультатВопроса, ДополнительныеПараметры) Экспорт
	Если РезультатВопроса = КодВозвратаДиалога.Нет Тогда
		Возврат;
	КонецЕсли;
	ТекущиеДанные = Элементы.СписокФайлов.ТекущиеДанные;
	Если ТекущиеДанные = Неопределено Тогда
		Возврат;
	КонецЕсли;
	УдалитьПапкуИлиФайлНаСервере(ТекущееРасположение + ТекущиеДанные.Имя);
	ПодключитьОбработчикОжидания("ПолучитьСписокФайловНаКлиенте", 1, Истина);
КонецПроцедуры

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

&НаКлиенте
Процедура ОткрытьПапкуНаКлиенте()
	ТекущиеДанные = Элементы.СписокФайлов.ТекущиеДанные;
	Если ТекущиеДанные = Неопределено Или ТекущиеДанные.Тип = "file" Тогда
		Возврат;
	КонецЕсли;
	ТекущееРасположение = ТекущиеДанные.Путь + "/";
	ПолучитьСписокФайловНаСервере();
КонецПроцедуры

&НаКлиенте
Процедура ПолучитьСписокФайловНаКлиенте()
	ПолучитьСписокФайловНаСервере();
КонецПроцедуры

&НаСервере
Процедура ЗагрузитьФайлНаСервере(ВыбранныйФайл)
	ОбработкаОбъект = РеквизитФормыВЗначение("Объект");
	ОбработкаОбъект.ЗагрузитьФайл(ВыбранныйФайл.Путь, ВыбранныйФайл.АдресХранилища);
КонецПроцедуры

&НаСервере
Процедура СоздатьПапкуНаСервере(Знач Путь)
	ОбработкаОбъект = РеквизитФормыВЗначение("Объект");
	ОбработкаОбъект.СоздатьПапку(Путь);
КонецПроцедуры

&НаСервере
Процедура УдалитьПапкуИлиФайлНаСервере(Путь)
	ОбработкаОбъект = РеквизитФормыВЗначение("Объект");
	ОбработкаОбъект.УдалитьПапкуИлиФайл(Путь);
КонецПроцедуры

&НаСервере
Функция СкачатьФайлНаСервере(Путь)
	ОбработкаОбъект = РеквизитФормыВЗначение("Объект");
	Возврат ОбработкаОбъект.СкачатьФайл(Путь);
КонецФункции

&НаСервере
Процедура ПолучитьСписокФайловНаСервере()
	ОбъектОбработки = РеквизитФормыВЗначение("Объект");
	ОбъектОбработки.ПолучитьСписокФайлов(ТекущееРасположение);
	ЗначениеВРеквизитФормы(ОбъектОбработки, "Объект");
КонецПроцедуры

&НаСервере
Процедура ПолучитьТокенНаСервере()
	ОбъектОбработки = РеквизитФормыВЗначение("Объект");
	ОбъектОбработки.ПолучитьТокен();
	ЗначениеВРеквизитФормы(ОбъектОбработки, "Объект");
КонецПроцедуры

#КонецОбласти
